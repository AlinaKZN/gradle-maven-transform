buildscript {
	repositories {
		mavenCentral()
	}
	def pluginVersion = file('../../version.txt').text.trim()
	dependencies {
		classpath "com.lazan:gradle-maven-transform:$pluginVersion"
	}
}

group = 'com.lazan'
version = file('../../version.txt').text.trim()

apply plugin: 'com.lazan.maven-transform'
apply plugin: 'java'

mavenTransform {
	pomXmls fileTree('../maven-root').matching {
		include '*/pom.xml'
	}
	outputDirectory "$buildDir/mavenTransform"

	projectsTransform {
		outputPath "settings.gradle"
		freemarkerTemplate 'settings-projects.ftl'
		context 'modelCount', { manyContext ->
			manyContext.projects.size()
		}
		classpath files('src/main/resources')
	}

	projectTransform {
		outputPath { singleContext -> "${singleContext.artifactId}.gradle".toString() }
		freemarkerTemplate 'project-dependencies.ftl'
		classpath files('src/main/resources')
	}
}

jar {
	dependsOn mavenTransform
	from mavenTransform.outputDirectory
}

artifacts {
	archives jar
}

build.dependsOn jar
